// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int kb;

    /** Initializes the keyboard. */
    function void init() {
        let kb = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(kb);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key_pressed;
        var char c;
        let key_pressed = 0;
        while (key_pressed = 0) {
            let key_pressed = Keyboard.keyPressed();
        }
        let c = key_pressed;
        while (~(key_pressed = 0)) {
            let key_pressed = Keyboard.keyPressed();
        }
        if (c = 128) {
            do Output.println();
        } else {
            if (c = 129) {
                do Output.backSpace();
            } else {
                do Output.printChar(c);
            }
        }
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String line;
        var char c;
        do Output.printString(message);
        let line = String.new(100);
        while (true) {
            let c = Keyboard.readChar();
            if (c = 128) {
                // if c is a newline character
                return line;
            } else {
                if (c = 129) {
                    // if c is a backspace
                    do line.eraseLastChar();
                } else {
                    let line = line.appendChar(c);
                }
            }
        }
        // This won't be reached
        do Sys.error(1);
        return line;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String line;
        var int int_value;
        let line = Keyboard.readLine(message);
        let int_value = line.intValue();
        do line.dispose();
        return int_value;
    }
}
